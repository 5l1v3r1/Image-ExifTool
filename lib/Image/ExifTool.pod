#------------------------------------------------------------------------------
# File:         ExifTool.pod - Documentation for File::ExifTool
#
# Description:  Read and write meta information in images
#
# URL:          http://owl.phy.queensu.ca/~phil/exiftool/
#
# Legal:        Copyright (c) 2003-2005 Phil Harvey (phil at owl.phy.queensu.ca)
#               This library is free software; you can redistribute it and/or
#               modify it under the same terms as Perl itself.
#------------------------------------------------------------------------------

=head1 NAME

Image::ExifTool - Read and write meta information in images

=head1 SYNOPSIS

  use Image::ExifTool 'ImageInfo';

  # ---- Simple procedural usage ----

  # Get hash of meta information tag names/values from an image
  $info = ImageInfo('a.jpg');

  # ---- Object-oriented usage ----

  # Create a new Image::ExifTool object
  $exifTool = new Image::ExifTool;

  # Extract meta information from an image
  $exifTool->ExtractInfo($file, \%options);

  # Get list of tags in the order they were found in the file
  @tagList = $exifTool->GetFoundTags('File');

  # Get the value of a specified tag
  $value = $exifTool->GetValue($tag, $type);

  # Get a tag description
  $description = $exifTool->GetDescription($tag);

  # Get the group name associated with this tag
  $group = $exifTool->GetGroup($tag, $family);

  # Set a new value for a tag
  $exifTool->SetNewValue($tag, $newValue);

  # Write new meta information to a file
  $success = $exifTool->WriteInfo($srcfile, $dstfile);

  # ...plus a host of other useful methods...

=head1 DESCRIPTION

ExifTool provides an extensible set of perl modules to read and write meta
information in image files.  It reads EXIF, GPS, IPTC, XMP, JFIF, GeoTIFF,
ICC Profile, Photoshop IRB and ID3 meta information from JPEG, JP2, TIFF,
GIF, BMP, PICT, QTIF, PNG, MNG, JNG, MIFF, PGM, PGM, PBM, XMP, EPS, PS, AI,
PDF, PSD, DCM, ACR, THM, CRW (Canon RAW), CR2 (Canon RAW 2), MRW (Minolta
RAW), NEF (Nikon Electronic image Format), PEF (Pentax RAW), ORF (Olympus
RAW Format), RAF (FujiFilm RAW Format), SRF (Sony Raw Format), MOS (Leaf
Mosaic), X3F (Sigma Raw) and DNG (Digital Negative) images, MP3 and WAV
audio files, and MOV videos. ExifTool also extracts information from the
maker notes of many digital cameras by various manufacturers including
Canon, Casio, FujiFilm, Kodak, Leaf, Minolta/Konica-Minolta, Nikon,
Olympus/Epson, Panasonic/Leica, Pentax/Asahi, Ricoh, Sanyo and Sigma/Foveon.
It writes EXIF, GPS, IPTC, XMP and MakerNotes information to JPEG, TIFF,
GIF, PPM, PGM, PBM, PNG, MNG, JNG, CRW, THM, CR2, MRW, NEF, PEF, MOS and DNG
files.

=head1 METHODS

=head2 new

Creates a new ExifTool object.

    $exifTool = new Image::ExifTool;

Note that ExifTool uses AUTOLOAD to load non-member methods, so any class
using Image::ExifTool as a base class must define an AUTOLOAD which calls
Image::ExifTool::DoAutoLoad().  ie)

    sub AUTOLOAD
    {
        Image::ExifTool::DoAutoLoad($AUTOLOAD, @_);
    }

=head2 ImageInfo

Obtain meta information from image.  This is the one step function for
obtaining meta information from an image.  Internally, L</ImageInfo> calls
L</ExtractInfo> to extract the information, L</GetInfo> to generate the
information hash, and L</GetTagList> for the returned tag list.

    # Return meta information for 2 tags only (procedural)
    $info = ImageInfo($filename, $tag1, $tag2)

    # Return information about an open image file (object-oriented)
    $info = $exifTool->ImageInfo(\*FILE)

    # Return information from image data in memory for specified tags
    $info = ImageInfo(\$imageData, \@tagList, \%options)

    # Extract information from an embedded thumbnail image
    $info = ImageInfo('image.jpg', 'thumbnailimage');
    $thumbInfo = ImageInfo($$info{ThumbnailImage});

=over 4

=item Inputs:

L</ImageInfo> is very flexible about the input arguments, and interprets
them based on their type.  It may be called with one or more arguments.
The one required argument is either a SCALAR (the image file name), a file
reference (a reference to the image file) or a SCALAR reference (a
reference to the image in memory).  Other arguments are optional.  The
order of the arguments is not significant, except that the first SCALAR is
taken to be the file name unless a file reference or scalar reference came
earlier in the argument list.

Below is an explanation of how the L</ImageInfo> function arguments are
interpreted:

=over 4

=item ExifTool ref

L</ImageInfo> may be called with an ExifTool object if desired.  The
advantage of using the object-oriented form is that the options may be set
before calling L</ImageInfo>, and the object may be used afterward to access
member functions.

=item SCALAR

The first scalar argument is taken to be the file name unless an earlier
argument specified the image data via a file reference (file ref) or data
reference (SCALAR ref). The remaining scalar arguments are names of tags for
requested information.  If no tags are specified, all possible information
is extracted.  Tag names may begin with '-' indicating tags to exclude.  The
tag names are case-insensitive, so note that the returned tags may not be
exactly the same as the requested tags. For this reason it is best to use
either the keys of the returned hash or the elements of the tag array when
accessing the return values.  See
L<Image::ExifTool::TagNames|Image::ExifTool::TagNames> for a complete list
of ExifTool tag names.

=item File ref

A reference to an open image file.  If you use this method (or a SCALAR
reference) to access information in an image, the FileName and Directory
tags will not be returned.  (Also, the FileSize and FileModifyDate tags will
not be returned unless it is a plain file.)

=item SCALAR ref

A reference to image data in memory.

=item ARRAY ref

Reference to a list of tag names.  On entry, any elements in the list are
added to the list of requested tags.  Tags with names beginning with '-' are
excluded.  On return, this list is updated to contain a sorted list of tag
names in the proper case.

=item HASH ref

Reference to a hash containing the options settings.  See L</Options>
documentation below for a list of available options.  Options specified
as arguments to L</ImageInfo> take precedence over L</Options> settings.

=back

=item Return Values:

L</ImageInfo> returns a reference to a hash of tag/value pairs. The keys of
the hash are the tag identifiers, which are similar to the tag names but my
have an embedded copy number if more than one tag with that name was found
in the image.  Use L</GetTagName> to remove the copy number from the tag.
Note that the case of the tags may not be the same as requested. Here is a
simple example to print out the information returned by L</ImageInfo>:

    foreach (sort keys %$info) {
        print "$_ => $$info{$_}\n";
    }

Values of the returned hash are usually simple scalars, but a scalar
reference is used to indicate binary data and an array reference may be used
to indicate a list.  Lists of values are joined by commas into a single
string if and only if the PrintConv option is enabled and the List option is
disabled (which are the defaults).  Note that binary values are not
necessarily extracted unless specifically requested or the Binary option is
set.  If not extracted the value is a reference to a string of the form
"Binary data ##### bytes".

The code below gives an example of how to handle these return values, as
well as illustrating the use of other ExifTool functions:

    use Image::ExifTool;
    my $exifTool = new Image::ExifTool;
    $exifTool->Options(Unknown => 1);
    my $info = $exifTool->ImageInfo('a.jpg');
    my $group = '';
    my $tag;
    foreach $tag ($exifTool->GetFoundTags('Group0')) {
        if ($group ne $exifTool->GetGroup($tag)) {
            $group = $exifTool->GetGroup($tag);
            print "---- $group ----\n";
        }
        my $val = $info->{$tag};
        if (ref $val eq 'SCALAR') {
            if ($$val =~ /^Binary data/) {
                $val = "($$val)";
            } else {
                my $len = length($$val);
                $val = "(Binary data $len bytes)";
            }
        }
        printf("%-32s : %s\n", $exifTool->GetDescription($tag), $val);
    }

As well as tags representing information extracted from the image,
the following tags generated by ExifTool may be returned:

    ExifToolVersion - The ExifTool version number.

    Error - An error message if the image could not be read.

    Warning - A warning message if problems were encountered
              while extracting information from the image.

=back

=head2 Options

Get/set ExifTool options.  This function can be called to set the default
options for an ExifTool object.  Options set this way are in effect for
all function calls but may be overridden by options passed as arguments
to a specific function.

    # Exclude the 'OwnerName' tag from returned information
    $exifTool->Options(Exclude => 'OwnerName');

    # Only get information in EXIF or MakerNotes groups
    $exifTool->Options(Group0 => ['EXIF', 'MakerNotes']);

    # Ignore information from IFD1
    $exifTool->Options(Group1 => '-IFD1');

    # Sort by groups in family 2, and extract unknown tags
    $exifTool->Options(Sort => 'Group2', Unknown => 1);

    # Do not extract duplicate tag names
    $oldSetting = $exifTool->Options(Duplicates => 0);

    # Get current setting
    $isVerbose = $exifTool->Options('Verbose');

=over 4

=item Inputs:

0) ExifTool object reference.

1) Option parameter name.

2) [optional] Option parameter value.

3-N) [optional] Additional parameter/value pairs.

=item Option Parameters:

=over 4

=item Binary

Flag to extract the value data for all binary tags.  Tag values representing
large binary data blocks (ie. ThumbnailImage) are not necessarily extracted
unless this option is set or the tag is specifically requested by name.
Default is 0.

=item ByteOrder

The byte order for newly created EXIF segments when writing. If EXIF
information already exists, the existing order is used instead.  Valid
values are 'MM', 'II' and undef.  If not defined, the order of the maker
notes is used (if maker notes are copied), otherwise 'MM' is used. Default
is undef.

=item Charset

Character set for converting Unicode character strings.  Valid values are:

  UTF8  - UTF-8 characters (the default)
  Latin - Windows Latin1 (cp1252)

=item Compact

Write compact output.  Default is 0.  Some data formats (XMP, IPTC) specify
that the data be padded with blanks to allow in-place editing.  By setting
this flag, 2kB is saved for files with XMP data, and 100 bytes for IPTC.

=item Composite

Flag to calculate Composite tags automatically.  Default is 1.

=item CoordFormat

Format for printing GPS coordinates.  This is a printf format string with
specifiers for degrees, minutes and seconds in that order, however minutes
and seconds may be omitted.  For example, the following table gives the
output for the same coordinate using various formats:

        CoordFormat              Output
    -------------------    ------------------
    q{%d deg %d' %.2f"}    54 deg 59' 22.80"   (the default)
    q{%d deg %.4f min}     54 deg 59.3800 min
    q{%.6f degrees}        54.989667 degrees

=item DateFormat

Format for printing EXIF date/time.  See C<strftime> in the L<POSIX> package
for details about the format string.  The default format is equivalent to
"%Y:%m:%d %H:%M:%S".

=item Duplicates

Flag to preserve values of duplicate tags (instead of overwriting existing
value).  Default is 1.

=item Exclude

Exclude specified tags from tags extracted from an image. The option value
is either a tag name or reference to a list of tag names to exclude.  The
case of tag names is not significant. This option is ignored for
specifically requested tags.  Tags may also be excluded by preceeding their
name with a '-' in the arguments to L</ImageInfo>.

=item IgnoreMinorErrors

Causes minor errors to be downgraded to warnings, and minor warnings to be
ignored.  This option is provided mainly to allow writing of files when
minor errors occur, but also allows thumbnail and preview images to be
extracted even if they don't have a recognizable header.  Minor
errors/warnings are denoted by '[minor]' at the start of the message.

=item Group#

Extract tags only for specified groups in family # (Group0 assumed if #
not given).  The option value may be a single group name or a reference
to a list of groups.  Case is significant in group names.  Specify a
group to be excluded by preceeding group name with a '-'.  See
L</GetAllGroups> for a list of available groups.

=item List

Flag to extract lists of PrintConv values into arrays instead of
concatinating them into comma-separated strings.  Default is 0.

=item MakerNotes

Flag to cause MakerNotes data and other writable subdirectories (such as
PrintIM) to be extracted as a data block.

=item PrintConv

Flag to enable automatic print conversion.  Also enables inverse
print conversion for writing.  Default is 1.

=item Sort

Specifies order to sort tags in returned list:

  Alpha  - Sort alphabetically
  File   - Sort in order that tags were found in the file
  Group# - Sort by tag group, where # is the group family
           number.  If # is not specified, Group0 is assumed.
           See GetGroup for a list of groups.
  Input  - Sort in same order as input tag arguments (default)

=item Unknown

Flag to get the values of unknown tags.  If set to 1, unknown tags are
extracted from EXIF directories.  If set to 2, unknown tags are also
extracted from binary data blocks.  Default is 0.

=item Verbose

Flag to print verbose messages.  May be set to a value from 0 to 5 to be
increasingly verbose.  Default is 0.  With the verbose option set, messages
are printed to the console as the file is parsed.  Level 1 prints the tag
names and raw values.  Level 2 adds more details about the tags.  Level 3
adds a hex dump of the tag data, but with limits on the number of bytes
dumped.  Levels 4 and 5 remove the dump limit on tag values and JPEG segment
data respectively.

=back

=item Return Values:

The original value of the last specified parameter.

=back

=head2 ClearOptions

Reset all options to their default values.

    $exifTool->ClearOptions();

=over 4

=item Inputs:

0) ExifTool object reference

=item Return Values:

(none)

=back

=head2 ExtractInfo

Extract all meta information from an image.

    $success = $exifTool->ExtractInfo('image.jpg', \%options);

=over 4

=item Inputs:

L</ExtractInfo> takes exactly the same arguments as L</ImageInfo>.  The only
difference is that a list of tags is not returned if an ARRAY reference is
given.  The following options are effective in the call to L</ExtractInfo>:

Binary, Composite, DateFormat, Unknown and Verbose.

=item Return Value:

1 if image was valid, 0 otherwise (and 'Error' tag set).

=back

=head2 GetInfo

L</GetInfo> is called to return meta information after it has been extracted
from the image by a previous call to L</ExtractInfo> or L</ImageInfo>. This
function may be called repeatedly after a single call to L</ExtractInfo> or
L</ImageInfo>.

    # Get image width and hieght only
    $info = $exifTool->GetInfo('ImageWidth', 'ImageHeight');

    # Get information for all tags in list (list updated with tags found)
    $info = $exifTool->GetInfo(\@ioTagList);

    # Get all information in Author or Location groups
    $info = $exifTool->GetInfo({Group2 => ['Author', 'Location']});

=over 4

=item Inputs:

Inputs are the same as L</ExtractInfo> and L</ImageInfo> except that an
image can not be specified.  Options in effect are:

Duplicates, Exclude, Group#, PrintConv (and Sort if tag list reference is
given).

=item Return Value:

Reference to information hash, the same as with L</ImageInfo>.

=back

=head2 WriteInfo

Write meta information to a file.  The specified source file is rewritten to
the same-type destination file with new information as specified by previous
calls to L</SetNewValue>.  The necessary segments and/or directories are
created in the destination file as required to store the specified
information.  May be called repeatedly to write the same information to
additional files without the need to call L</SetNewValue> again.

    # add information to a source file, writing output to new file
    $exifTool->WriteInfo($srcfile, $dstfile);

    # create XMP data file from scratch
    $exifTool->WriteInfo(undef, $dstfile, 'XMP');

    # edit file in place (you do have backups, right?)
    $exifTool->WriteInfo($srcfile);

=over 4

=item Inputs:

0) ExifTool object reference

1) Source file name, file reference, or scalar reference

2) [optional] Destination file name, file or scalar reference

3) [optional] Destination file type

=item Return Value:

1 if file was written OK, 2 if file was written but no changes made, 0 on
file write error.

If an error code is returned, an Error tag is set and GetValue('Error') can
be called to obtain the error description.  A Warning tag mag be set even if
this routine is successful.

    $errorMessage = $exifTool->GetValue('Error');
    $warningMessage = $exifTool->GetValue('Warning');

=item Notes:

The source file name may be undefined to create a file from scratch
(currently only .XMP files can be created in this way).  If undefined, the
destination file type is required unless the type can be determined from the
destination file name.

The destination file name may be undefined to edit a file in place (make
sure you have backups!).  If a destination file name is given, the specified
file must not exist because existing destination files will not be
overwritten.

The destination file type is only used if the source file is undefined.

=back

=head2 CombineInfo

Combine information from more than one information hash into a single hash.

    $info = $exifTool->CombineInfo($info1, $info2, $info3);

=over 4

=item Inputs:

0) ExifTool object reference

1-N) Information hash references

=back

If the Duplicates option is disabled and duplicate tags exist, the order of
the hashes is significant.  In this case, the value used is the first value
found as the hashes are scanned in order of input.  The Duplicates option
is the only option that is in effect for this function.

=head2 GetTagList

Get a sorted list of tags from the specified information hash or tag list.

    @tags = $exifTool->GetTagList($info, 'Group0');

=over 4

=item Inputs:

0) ExifTool object reference,

1) [optional] Information hash reference or tag list reference,

2) [optional] Sort order ('File', 'Input', 'Alpha' or 'Group#').

If the information hash or tag list reference is not provided, then the list
of found tags from the last call to L</ImageInfo>, L</ExtractInfo> or
L</GetInfo> is used instead, and the result is the same as if
L</GetFoundTags> was called.  If sort order is not specified, the sort order
is taken from the current options settings.

=item Return Values:

A list of tags in the specified order.

=back

=head2 GetFoundTags

Get list of found tags in specified sort order.  The found tags are the tags
for the information obtained from the most recent call to L</ImageInfo>,
L</ExtractInfo> or L</GetInfo> for this object.

    @tags = $exifTool->GetFoundTags('File');

=over 4

=item Inputs:

0) ExifTool object reference

1) [optional] Sort order ('File', 'Input', 'Alpha' or 'Group#')

If sort order is not specified, the sort order from the ExifTool options is
used.

=item Return Values:

A list of tags in the specified order.

=back

=head2 GetRequestedTags

Get list of requested tags.  These are the tags that were specified in the
arguments of the most recent call to L</ImageInfo>, L</ExtractInfo> or
L</GetInfo>, including tags specified via a tag list reference. Shortcut
tags are expanded in the list.

    @tags = $exifTool->GetRequestedTags();

=over 4

=item Inputs:

(none)

=item Return Values:

List of requested tags in the same order that they were specified.
Note that this list will be empty if tags were not specifically requested
(ie. If extracting all tags).

=back

=head2 GetValue

Get the value of a specified tag.  The returned value is either the
human-readable (PrintConv) value, the converted machine-readable (ValueConv)
value, or the original raw (Raw) value.  If the value type is not specified,
the PrintConv value is returned if the PrintConv option is set, otherwise
the ValueConv value is returned.

In the case of a list (as can happen with the 'Keywords' tag for instance),
PrintConv returns a comma-separated string of values in scalar context,
while ValueConv returns a reference to an array of values.  In list context,
L</GetValue> returns the list for both PrintConv and ValueConv.  The
PrintConv values are the values returned by L</ImageInfo> and L</GetInfo> in
the tag/value hash unless the PrintConv option is disabled.

    # PrintConv example
    my $val = $exifTool->GetValue($tag);
    if (ref $val eq 'SCALAR') {
        print "$tag = (unprintable value)\n";
    } else {
        print "$tag = $val\n";
    }

    # ValueConv examples
    my $val = $exifTool->GetValue($tag, 'ValueConv');
    if (ref $val eq 'ARRAY') {
        print "$tag is a list of values\n";
    } elsif (ref $val eq 'SCALAR') {
        print "$tag represents binary data\n";
    } else {
        print "$tag is a simple scalar\n";
    }

    my @keywords = $exifTool->GetValue('Keywords', 'ValueConv');

=over 4

=item Inputs:

0) ExifTool object reference

1) Tag key

2) [optional] Value type, 'PrintConv', 'ValueConv', 'Both' or 'Raw'

The default value type is 'PrintConv' if the PrintConv option is set,
otherwise the default is 'ValueConv'.  A value type of 'Both' returns both
ValueConv and PrintConv values as a list.

=item Return Values:

The value of the specified tag.  If the tag represents a list of values and
the List option is disabled then PrintConv returns a comma separated string
of values, otherwise a reference to the list is returned in scalar context.
The list itself is returned in list context.  Values may also be scalar
references to binary data.

Note: It is possible for L</GetValue> to return an undefined ValueConv or
PrintConv value (or an empty list in list context) even if the tag exists,
since it is possible for these conversions to yield undefined values.

=back

=head2 SetNewValue

Set the new value for a tag.  The new value is the value that will be
written for this tag in subsequent calls to L</WriteInfo>.

For list-type tags (like Keywords), call repeatedly with the same tag name
for each value in the list.

    $success = $exifTool->SetNewValue($tag, $value);

    ($success, $errStr) = $exifTool->SetNewValue($tag, $value);

    # delete a tag (also resets AddValue and DelValue options for this tag)
    $exifTool->SetNewValue($tag);

    # reset all values from previous calls to SetNewValue()
    $exifTool->SetNewValue();

    # delete a single keyword
    $exifTool->SetNewValue('Keywords', $word, DelValue => 1);

    # add a keyword without replacing existing keywords
    $exifTool->SetNewValue(Keywords => $word, AddValue => 1);

    # set a tag in a specific group
    $exifTool->SetNewValue(Headline => $val, Group => 'XMP');

    # shift original date/time back by 1 hour
    $exifTool->SetNewValue(DateTimeOriginal => '1:00', Shift => -1);

=over 4

=item Inputs:

0) ExifTool object reference

1) [optional] Tag key or tag name, or undefined to clear all new values.  A
tag name of '*' can be used when deleting tags to delete all tags, or all
tags in a specified group.  The tag name may be prefixed by group name,
separated by a colon (ie. 'GROUP:TAG'), which is equivalent to using a
'Group' option argument.

2) [optional] New value for tag.  Undefined to delete tag from file.

3-N) [optional] SetNewValue options hash entries (see below)

=item SetNewValue Options:

=over 4

=item Type

The type of value being set.  Valid values are PrintConv, ValueConv or Raw.
Default is PrintConv if the L<PrintConv> Option is set, otherwise ValueConv.

=item AddValue

Specifies that the value be added to an existing list rather than replacing
the list.  Valid values are 0 or 1.  Default is 0.

=item DelValue

Delete the exisiting tag if it has the specified value.  Valid values are
0 or 1.  Default is 0.

=item Group

Specifies group name where tag should be written.  If not specified, tag is
written to hightest priority group as specified by L</SetNewGroups>.  Any
family 0 or 1 group name may be used.  Case is not significant.

=item Protected

Bit mask for tag protection levels to write.  Bit 0x01 allows writing of
'unsafe' tags.  Bit 0x02 allows writing of 'protected' tags, and should only
be used internally by ExifTool.  See
L<Image::ExifTool::TagNames|Image::ExifTool::TagNames>, for a list of tag
names indicating 'unsafe' and 'protected' tags.  Default is 0.

=item Replace

Flag to replace the previous new value for this tag (ie. replace the value
set in a previous call to L</SetNewValue>).  Valid values are 0 (don't
repace), 1 (replace with specified new value) or 2 (reset previous new value
only).

=item Shift

Shift the tag by the specified value.  Currently only date/time tags can be
shifted.  Undefined for no shift, 1 for a positive shift, or -1 for a
negative shift.  If 0, the shift is applied only if the tag is shiftable.
In this case, the direction of the shift is positive if AddValue is set, or
negative if DelValue is set.  Default is undef.  See
L<Image::ExifTool::Shift.pl|Image::ExifTool::Shift.pl> for more information.

=back

=item Return Values:

The number of tags set and prints any errors in scalar context, or the
number of tags set and the error string in list context.

=back

=head2 SetNewValuesFromFile

A very powerful routine that sets new values for tags from information found
in a specified file.

    $info = $exifTool->SetNewValuesFromFile($srcFile, @tags);

=over 4

=item Inputs:

0) ExifTool object reference

1) File name, file reference, or scalar reference

2-N) [optional] List of tag names to set.  All writable tags are set if none
are specified.  The tag names are not case sensitive, and may be prefixed by
an optional family 0 or 1 group name, separated by a colon (ie. 'exif:iso').
A leading '-' indicates tags to be excuded (ie. '-comment').  An asterisk
('*') may be used for the tag name, and is useful when a group is specified
to set all tags from a group (ie. 'XMP:*').  A special feature allows tag
names of the form 'SRCTAG>DSTTAG' (or 'DSTTAGE<lt>SRCTAG') to be specified
to copy information to a tag with a different name or a specified group.
Both 'SRCTAG' and 'DSTTAG' may use '*' and/or be prefixed by a group name
(ie. 'modifyDate>fileModifyDate' or '*>xmp:*').  Tags are evaluated in
order, so exclusions apply only to tags included earlier in the list.

By default, this routine will commute information between same-named tags in
different groups, allowing information to be translated between images with
different formats.  This behaviour may be modified by specifying a group
name for extracted tags (even if '*' is used as a group name), in which case
the information is written to the original group, unless redirected to a
different group.  (For example, a tag name of '*:*' may be specified to copy
all information while preserving the original groups.)

=item Return Values:

A hash of information that was set successfully.  May include Warning or
Error entries if there were problems reading the input file.

=item Notes:

The PrintConv option applies to this routine, but it should normally be left
on to provide more reliable transfer of information between groups.

If a preview image exists, it is not copied.  The preview image must be
transferred separately if desired.

=back

=head2 GetNewValues

Get list of new Raw values for the specified tag.  These are the values
that will be written to file.  Only tags which support a 'List' may return
more than one value.

    $rawVal = $exifTool->GetNewValues($tag);

    @rawVals = $exifTool->GetNewValues($tag);

=over 4

=item Inputs:

0) ExifTool object reference

1) Tag key or tag name

=item Return Values:

List of new Raw tag values.  The list may be empty if the tag is being
deleted (ie. if SetNewValue was called without a value).

=back

=head2 CountNewValues

Return the total number of new values set.

    $numSet = $exifTool->CountNewValues();

=over 4

=item Inputs:

0) ExifTool object reference

=item Return Values:

The total number of tags with new values set.

=back

=head2 SaveNewValues

Save state of new values to be later restored by L</RestoreNewValues>.

    $exifTool->SaveNewValues();         # save state of new values
    $exifTool->SetNewValue(ISO => 100); # set new value for ISO
    $exifTool->WriteInfo($src, $dst1);  # write ISO + previous new values
    $exifTool->RestoreNewValues();      # restore previous new values
    $exifTool->WriteInfo($src, $dst2);  # write previous new values only

=over 4

=item Inputs:

0) ExifTool object reference

=item Return Value:

None.

=back

=head2 RestoreNewValues

Restore new values to the settings that existed when L</SaveNewValues> was
last called.  May be called repeatedly after a single call to
L</SaveNewValues>.  See L</SaveNewValues> above for an example.

=over 4

=item Inputs:

0) ExifTool object reference

=item Return Value:

None.

=back

=head2 SetFileModifyDate

Set the file modification time from the new value of the FileModifyDate tag.

    $result = $exifTool->SetFileModifyDate($file);

=over 4

=item Inputs:

0) ExifTool object reference

1) File name

2) [optional] Base time if applying shift (days before $^T)

=item Return Value:

1 if the time was changed, 0 if the FileModifyDate tag wasn't set, or -1 if
there was an error setting the time.

=back

=head2 SetNewGroups

Set the order of the preferred groups when adding new information.  In
subsequent calls to L</SetNewValue>, new information will be created in the
first valid group of this list.  This has an impact only if the group is not
specified and the tag name exists in more than one group.  The default order
is EXIF, GPS, IPTC, XMP and MakerNotes.  The default order is EXIF, GPS,
IPTC, XMP and MakerNotes.  Any family 0 group name may be used.  Case is not
significant.

    $exifTool->SetNewGroups('XMP','EXIF','IPTC');

=over 4

=item Inputs:

0) ExifTool object reference

1-N) Groups in order of priority.  If no groups are specified, the priorities
are reset to the defaults.

=item Return Value:

None.

=back

=head2 GetNewGroups

Get current group priority list.

    @groups = $exifTool->GetNewGroups();

=over 4

=item Inputs:

0) ExifTool object reference

=item Return Values:

List of group names in order of write priority.  Highest priority first.

=back

=head2 GetTagID

Get the ID for the specified tag.  The ID is the IFD tag number in EXIF
information, the property name in XMP information, or the data offset in a
binary data block. For some tags, such as Composite tags where there is no ID,
an empty string is returned.

    $id = $exifTool->GetTagID($tag);

=over 4

=item Inputs:

0) ExifTool object reference

1) Tag key

=item Return Values:

Tag ID or '' of there is no ID for this tag.

=back

=head2 GetDescription

Get description for specified tag.  This function will always return a
defined value.  In the case where the description doesn't exist, the tag
name is returned.

=over 4

=item Inputs:

0) ExifTool object reference

1) Tag key

=item Return Values:

A description for the specified tag.

=back

=head2 GetGroup

Get group name for specified tag.

    $group = $exifTool->GetGroup($tag, 0);

=over 4

=item Inputs:

0) ExifTool object reference

1) Tag key

2) [optional] Group family number

=item Return Values:

Group name (or 'Other' if tag has no group).  If no group family is
specified, L</GetGroup> returns the name of the group in family 0 when
called in scalar context, or the names of groups for all families in list
context. See L</GetAllGroups> for a list of groups in each famly.

=back

=head2 GetGroups

Get list of group names for specified information.

    @groups = $exifTool->GetGroups($info, 2);

=over 4

=item Inputs:

0) ExifTool object reference

1) [optional] Info hash ref (default is all extracted info)

2) [optional] Group family number (default 0)

=item Return Values:

List of group names in alphabetical order. If information hash is not
specified, the group names are returned for all extracted information.

=back

=head2 BuildCompositeTags

Builds composite tags from required tags.  The composite tags are
convenience tags which are derived from the values of other tags.  This
routine is called automatically by L</ImageInfo> and L</ExtractInfo> if the
Composite option is set.

=over 4

=item Inputs:

0) ExifTool object reference

=item Return Values:

(none)

=item Notes:

Tag values are calculated in alphabetical order unless a tag Require's or
Desire's another composite tag, in which case the calculation is deferred
until after the other tag is calculated. Composite tags may need to read
data from the image for their value to be determined, so for these
L</BuildCompositeTags> must be called while the image is available.  This is
only a problem if L</ImageInfo> is called with a filename (as opposed to a
file reference or scalar reference) since in this case the file is closed
before L</ImageInfo> returns.  However if you enable the Composite option,
L</BuildCompositeTags> is called from within L</ImageInfo> before the file
is closed.

=back

=head2 GetTagName [static]

Get name of tag from tag key.  This is a convenience function that
strips the embedded copy number, if it exists, from the tag key.

Note: "static" in the heading above indicates that the function does not
require an ExifTool object reference as the first argument.  All functions
documented below are also static.

    $tagName = Image::ExifTool::GetTagName($tag);

=over 4

=item Inputs:

0) Tag key

=item Return Value:

Tag name.  This is the same as the tag key but has the copy number removed.

=back

=head2 GetShortcuts [static]

Get a list of shortcut tags.

=over 4

=item Inputs:

(none)

=item Return Values:

List of shortcut tags (as defined in Image::ExifTool::Shortcuts).

=back

=head2 GetAllTags [static]

Get list of all available tag names.

    @tagList = Image::ExifTool::GetAllTags();

=over 4

=item Inputs:

(none)

=item Return Values:

A list of all available tags in alphabetical order.

=back

=head2 GetWritableTags [static]

Get list of all writable tag names.

    @tagList = Image::ExifTool::GetWritableTags();

=over 4

=item Inputs:

(none)

=item Return Values:

A list of all writable tags in alphabetical order.  These are the
tags for which the values may be set through L</SetNewValue>.

=back

=head2 GetAllGroups [static]

Get list of all group names in specified family.

    @groupList = Image::ExifTool::GetAllGroups($family);

=over 4

=item Inputs:

0) Group family number (0-2)

=item Return Values:

A list of all groups in the specified family in alphabetical order.

=back

Three families of groups are currently defined: 0, 1 and 2. Families 0 and 1
are based on the file structure, and family 2 classifies information based
on the logical category to which the information refers.

Families 0 and 1 are similar except that family 1 is more specific, and
sub-divides the EXIF, MakerNotes, XMP and ICC_Profile groups to give more
detail about the specific location where the information was found.  The
EXIF group is split up based on the specific IFD (Image File Directory), the
MakerNotes group is divided into groups for each manufacturer, and the XMP
group is separated based on the XMP namespace prefix.  Note that only common
XMP namespaces are listed below but additional namespaces may be present in
some XMP data.  Also note that the 'XMP-xmp...' group names may appear in
the older form 'XMP-xap...' since these names evolved as the XMP standard
was developed.  The ICC_Profile group is broken down to give information
about the specific ICC_Profile tag from which multiple values were
extracted. As well, information extracted from the ICC_Profile header is
separated into the ICC-header group.

Here is a complete list of groups for each family:

=over 4

=item Family 0 (General Location):

APP12, BMP, Canon, Composite, DICOM, EXIF, ExifTool, File, GPS, GeoTiff,
ICC_Profile, ID3, IPTC, JFIF, Jpeg2000, Leaf, MIFF, MNG, MakerNotes, Meta,
PDF, PICT, PNG, Pentax, Photoshop, PostScript, PrintIM, QuickTime, SigmaRaw,
WAV, XMP

=item Family 1 (Specific Location):

APP12, BMP, Canon, CanonCustom, CanonRaw, Casio, Composite, DICOM, ExifIFD,
ExifTool, File, FujiFilm, GPS, GeoTiff, GlobParamIFD, ICC-chrm, ICC-clrt,
ICC-header, ICC-meas, ICC-view, ICC_Profile, ID3, ID3v1, ID3v2_2, ID3v2_3,
ID3v2_4, IFD0, IFD1, IPTC, InteropIFD, JFIF, Jpeg2000, Kodak,
KodakBordersIFD, KodakEffectsIFD, Leaf, LeafSubIFD, MIFF, MNG, MakerUnknown,
MetaIFD, Minolta, Nikon, NikonCapture, NikonPreview, Olympus, PDF, PICT,
PNG, Panasonic, Pentax, Photoshop, PostScript, PrintIM, QuickTime, Ricoh,
SRF#, Sanyo, Sigma, SigmaRaw, Sony, SubIFD, Track#, WAV, XMP, XMP-PixelLive,
XMP-aux, XMP-cc, XMP-crs, XMP-dc, XMP-dex, XMP-exif, XMP-iptcCore, XMP-pdf,
XMP-photoshop, XMP-tiff, XMP-xmp, XMP-xmpBJ, XMP-xmpMM, XMP-xmpPLUS,
XMP-xmpRights, XMP-xmpTPg

=item Family 2 (Category):

Audio, Author, Camera, ExifTool, Image, Location, Other, Printing, Time,
Unknown

=back

=head2 GetFileType [static]

Get type of file given file name.

    my $type = Image::ExifTool::GetFileType($filename);

=over 4

=item Inputs:

0) [optional] File name (or just an extension)

=item Return Value:

A string, based on the file extension, which represents the type of file.
Returns undefined value if file type is not supported by ExifTool.  In array
context, may return more than one file type if the file may be different
formats.  Returns a list of extensions for all recognized file types if no
input extension is specified.

=back

=head2 CanWrite [static]

Can the specified file or file type be written?

    my $writable = Image::ExifTool::CanWrite($filename);

=over 4

=item Inputs:

0) File name, file extension, or file type

=item Return Value:

True if the specified file type can be written (edited).

=back

=head2 CanCreate [static]

Can the specified file or file type be created?

    my $creatable = Image::ExifTool::CanCreate($filename);

=over 4

=item Inputs:

0) File name, file extension, or file type

=item Return Value:

True if the specified file type can be created from scratch.  Currently,
this can only be done with XMP files.

=back

=head1 AUTHOR

Copyright 2003-2005, Phil Harvey

This library is free software; you can redistribute it and/or modify
it under the same terms as Perl itself.

=head1 CREDITS

Many people have helped in the development of ExifTool through their bug
reports, comments and suggestions, and/or additions to the code.  See
html/index.html in the Image::ExifTool distribution package for a list of
people who have contributed to this project.

=head1 SEE ALSO

L<exiftool(1)|exiftool>,
L<Image::ExifTool::TagNames(3pm)|Image::ExifTool::TagNames>,
L<Image::ExifTool::Shortcuts(3pm)|Image::ExifTool::Shortcuts>,
L<Image::ExifTool::Shift.pl|Image::ExifTool::Shift.pl>,
L<Image::Info(3pm)|Image::Info>,
L<Image::MetaData::JPEG(3pm)|Image::MetaData::JPEG>

=cut

# end
